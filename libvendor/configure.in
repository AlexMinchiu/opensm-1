dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(libosmvendor, 2.1.1, openib-general@openib.org)
AC_CONFIG_SRCDIR([osm_vendor_ibumad.c])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE

dnl the library version info is available in the file: libosmvendor.ver
osmvendor_api_version=`grep LIBVERSION $srcdir/libosmvendor.ver |sed 's/LIBVERSION=//'`
if test -z $osmvendor_api_version; then
   osmvendor_api_version=1:0:0
fi
AC_SUBST(osmvendor_api_version)

AC_ARG_ENABLE(libcheck, [  --disable-libcheck      do not test for presence of
ib libraries],
[       if test x$enableval = xno ; then
                disable_libcheck=yes
        fi
])

dnl Checks for programs
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LIBTOOL

dnl Select appropriate vendor type
OPENIB_APP_OSMV_SEL

dnl Checks for libraries
OPENIB_APP_OSMV_CHECK_LIB

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
OPENIB_APP_OSMV_CHECK_HEADER
AC_CHECK_HEADERS([fcntl.h stddef.h stdint.h sys/ioctl.h])

dnl Checks for library functions
AC_FUNC_CLOSEDIR_VOID
AC_CHECK_FUNCS([memset strerror strstr])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T

AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
    if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
        ac_cv_version_script=yes
    else
        ac_cv_version_script=no
    fi)

AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$ac_cv_version_script" = "yes")

dnl support debug mode
AC_ARG_ENABLE(debug,
[  --enable-debug Turn on debug mode],
[case "${enableval}" in
  yes) debug=true ;;
  no)  debug=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],[debug=false])
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

# we have to revive the env CFLAGS as some how they are being overwritten...
# see http://sources.redhat.com/automake/automake.html#Flag-Variables-Ordering
# for why they should NEVER be modified by the configure to allow for user
# overrides.
CFLAGS=$ac_env_CFLAGS_value

AC_CONFIG_FILES([Makefile libosmvendor.spec])
AC_OUTPUT
