dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(opensm, 3.1.6, general@lists.openfabrics.org)
AC_CONFIG_SRCDIR([opensm/osm_opensm.c])
AC_CONFIG_AUX_DIR(config)
AC_CONFIG_HEADERS(include/config.h)
AM_INIT_AUTOMAKE

AC_SUBST(RELEASE, ${RELEASE:-unknown})
AC_SUBST(TARBALL, ${TARBALL:-${PACKAGE}-${VERSION}.tar.gz})

dnl Defines the Language
AC_LANG_C

dnl Provides control over re-making of all auto files
dnl We also use it to define swig dependencies so end
dnl users do not see them.
AM_MAINTAINER_MODE

dnl Required for cases make defines a MAKE=make ??? Why
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_LIBTOOL
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_YACC
AC_PROG_LEX

dnl Checks for libraries
AC_CHECK_LIB(pthread, pthread_mutex_init, [],
	AC_MSG_ERROR([pthread_mutex_init() not found.  libosmcomp requires libpthread.]))

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME
AC_STRUCT_TM
AC_C_VOLATILE

dnl We use --version-script with ld if possible
AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
	ac_cv_version_script=yes
else
	ac_cv_version_script=no
fi)
AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$ac_cv_version_script" = "yes")

dnl Define an input config option to control debug compile
AC_ARG_ENABLE(debug,    [  --enable-debug          Turn on debugging],
[case "${enableval}" in
	yes) debug=true ;;
	no)  debug=false ;;
	*) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],debug=false)
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

AC_ARG_ENABLE(libcheck, [  --disable-libcheck      do not test for presence of ib libraries],
[if test x$enableval = xno ; then
	disable_libcheck=yes
fi])

dnl check if they want the socket console
OPENIB_OSM_CONSOLE_SOCKET_SEL

dnl select performance manager or not
OPENIB_OSM_PERF_MGR_SEL

dnl Check for a different subdir for the config files.
OPENSM_CONF_SUB_DIR=opensm
AC_MSG_CHECKING(for --with-opensm-conf-sub-dir)
AC_ARG_WITH(opensm-conf-sub-dir,
    AC_HELP_STRING([--with-opensm-conf-sub-dir=dir],
                   [define a directory name for opensm's conf files <sysconfdir>/<dir> (default "opensm")]),
    [ case "$withval" in
    no)
        ;;
    *)
        withopensmconfsubdir=yes
        OPENSM_CONF_SUB_DIR=$withval
        ;;
    esac ]
)
AC_MSG_RESULT(${withopensmconfsubdir=no})

dnl Set up <sysconfdir>/opensm config dir.
CONF_DIR_TMP1="`eval echo ${sysconfdir}/$OPENSM_CONF_SUB_DIR`"
CONF_DIR_TMP2="`echo $CONF_DIR_TMP1 | sed 's/^NONE/$ac_default_prefix/'`"
CONF_DIR="`eval echo $CONF_DIR_TMP2`"

dnl Check for a different default node name map file
NODENAMEMAPFILE=ib-node-name-map
AC_MSG_CHECKING(for --with-node-name-map )
AC_ARG_WITH(node-name-map,
    AC_HELP_STRING([--with-node-name-map=file],
                   [define a default node name map file (default ib-node-name-map)]),
    [ case "$withval" in
    no)
        ;;
    *)
        withnodenamemap=yes
        NODENAMEMAPFILE=$withval
        ;;
    esac ]
)
AC_MSG_RESULT(${withnodenamemap=no})
AC_DEFINE_UNQUOTED(HAVE_DEFAULT_NODENAME_MAP,
	["$CONF_DIR/$NODENAMEMAPFILE"],
	[Define a default node name map file])

dnl Check for a different QOS policy file
QOS_POLICY_FILE=qos-policy.conf
AC_MSG_CHECKING(for --with-qos-policy-conf)
AC_ARG_WITH(qos-policy-conf,
    AC_HELP_STRING([--with-qos-policy-conf=file],
                   [define a QOS policy config file (default qos-policy.conf)]),
    [ case "$withval" in
    no)
        ;;
    *)
        withqospolicyconf=yes
        QOS_POLICY_FILE=$withval
        ;;
    esac ]
)
AC_MSG_RESULT(${withqospolicyconf=no})
AC_DEFINE_UNQUOTED(HAVE_DEFAULT_QOS_POLICY_FILE,
	["$CONF_DIR/$QOS_POLICY_FILE"],
	[Define a QOS policy config file])

dnl select example event plugin or not
OPENIB_OSM_DEFAULT_EVENT_PLUGIN_SEL

dnl Provide user option to select vendor
OPENIB_APP_OSMV_SEL

dnl Checks for headers and libraries
OPENIB_APP_OSMV_CHECK_HEADER
OPENIB_APP_OSMV_CHECK_LIB

# we have to revive the env CFLAGS as some how they are being overwritten...
# see http://sources.redhat.com/automake/automake.html#Flag-Variables-Ordering
# for why they should NEVER be modified by the configure to allow for user
# overrides.
CFLAGS=$ac_env_CFLAGS_value

dnl Create the following Makefiles
AC_OUTPUT([include/opensm/osm_version.h Makefile include/Makefile complib/Makefile libvendor/Makefile opensm/Makefile osmeventplugin/Makefile osmtest/Makefile opensm.spec])
