dnl Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT(complib, 2.3.0, general@lists.openfabrics.org)
AC_CONFIG_SRCDIR([cl_spinlock.c])
AC_CONFIG_AUX_DIR(config)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE

dnl the library version info is available in the file: libosmcomp.ver
complib_api_version=`grep LIBVERSION $srcdir/libosmcomp.ver | sed 's/LIBVERSION=//'`
if test -z $complib_api_version; then
   complib_api_version=1:0:0
fi
AC_SUBST(complib_api_version)

dnl Checks for programs
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_PROG_LIBTOOL

dnl Checks for libraries
AC_CHECK_LIB(pthread, pthread_mutex_init, [],
	AC_MSG_ERROR([pthread_mutex_init() not found.  libosmcomp requires libpthread.]))

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h stdlib.h string.h sys/ioctl.h sys/time.h syslog.h unistd.h])

dnl Checks for library functions
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_CHECK_FUNCS([gettimeofday memset strerror])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME

dnl We use --version-script with ld if possible
AC_CACHE_CHECK(whether ld accepts --version-script, ac_cv_version_script,
    if test -n "`$LD --help < /dev/null 2>/dev/null | grep version-script`"; then
        ac_cv_version_script=yes
    else
        ac_cv_version_script=no
    fi)

AM_CONDITIONAL(HAVE_LD_VERSION_SCRIPT, test "$ac_cv_version_script" = "yes")

dnl Support debug mode build - if enable-debug provided the DEBUG variable is set
AC_ARG_ENABLE(debug,
[  --enable-debug Turn on debug mode],
[case "${enableval}" in
  yes) debug=true ;;
  no)  debug=false ;;
  *) AC_MSG_ERROR(bad value ${enableval} for --enable-debug) ;;
esac],[debug=false])
AM_CONDITIONAL(DEBUG, test x$debug = xtrue)

# we have to revive the env CFLAGS as some how they are being overwritten...
# see http://sources.redhat.com/automake/automake.html#Flag-Variables-Ordering
# for why they should NEVER be modified by the configure to allow for user
# overrides.
CFLAGS=$ac_env_CFLAGS_value


AC_CONFIG_FILES([Makefile libosmcomp.spec])
AC_OUTPUT
